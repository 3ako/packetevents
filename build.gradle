plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0' apply(false)
}

group = 'com.github.retrooper'
version = '2.0.0'
description = 'packetevents'
java.sourceCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

wrapper {
    gradleVersion = "7.3.1"
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.github.retrooper'
    version = '2.0.0'

    if (project.name != 'packetevents') {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId = 'com.github.retrooper.packetevents'
                    artifactId = project.name
                    from(components.java)
                }
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
    }
}
