plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'com.github.retrooper'
version = '2.0.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
}

shadowJar {
    dependencies {
        exclude(dependency("com.google.code.gson:gson:2.8.0"))
    }
    relocate("net.kyori", "com.github.retrooper.packetevents.libs.net.kyori")
    //Just for jitpack to work
    archiveVersion.set('')
    minimize()
}

dependencies {
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    implementation project(':netty-common')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.retrooper.packetevents'
            artifactId = 'bungeecord'
            from(components.java)
        }
    }
}

//So that SNAPSHOT is always the latest SNAPSHOT
configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

//So we only produce one jar with shadowJar. No suffixes. Just replace what the original jar would be named.
tasks.jar.configure {
    classifier = 'default'
}

tasks.shadowJar.configure {
    classifier = null
}