plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'com.github.retrooper'
version = '2.0.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
}

shadowJar {
    dependencies {
        exclude(dependency("com.google.code.gson:gson:2.8.0"))
    }
    //Just for jitpack to work
    archiveVersion.set('')
    minimize()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:22.0.0'
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    api project(':api')
    implementation project(':netty-common')
    api 'net.kyori:adventure-api:4.10.0'
    api 'net.kyori:adventure-text-serializer-gson:4.10.0'
    api 'net.kyori:adventure-text-serializer-legacy:4.10.0'
    //no gson as its on bungee already
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.retrooper.packetevents'
            artifactId = 'bungeecord'
            from(components.java)
        }
    }
}

//So that SNAPSHOT is always the latest SNAPSHOT
configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

//So we only produce one jar with shadowJar. No suffixes. Just replace what the original jar would be named.
tasks.jar.configure {
    classifier = 'default'
}

tasks.shadowJar.configure {
    classifier = null
}