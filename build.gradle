plugins {
    id 'java'
    id 'maven-publish'
}

group = 'io.github.retrooper'
version = '2.0.0'
description = 'packetevents'
java.sourceCompatibility = JavaVersion.VERSION_1_8

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        if (project.name != "packetevents") {
            compileOnly 'org.jetbrains:annotations:22.0.0'

            String[] adventureShadingModules = ["spigot", "bungeecord"]
            boolean isExporting = !project.hasProperty("library");
            if (!isExporting) {
                //If we aren't building standalone jars, just using it as a library, we shade nothing.
                adventureShadingModules = []
            }
            boolean shaded = false
            for (String s : adventureShadingModules) {
                if (project.name == s) {
                    api project(':adventure')
                    shaded = true
                    break
                }
            }
            if (!shaded && project.name != "adventure") {
                compileOnly project(':adventure')
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

wrapper {
    gradleVersion = "7.3.1"
    distributionType = Wrapper.DistributionType.ALL
}
